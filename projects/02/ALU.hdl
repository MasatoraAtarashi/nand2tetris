// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    //zx
    And16(a=x, b=false, out=zerox);
    Mux16(a=x, b=zerox, sel=zx, out=outx);

    //nx
    Not16(in=outx, out=negx);
    Mux16(a=outx, b=negx, sel=nx, out=outx2);

    //zy
    And16(a=y, b=false, out=zeroy);
    Mux16(a=y, b=zeroy, sel=zy, out=outy);

    //ny
    Not16(in=outy, out=negy);
    Mux16(a=outy, b=negy, sel=ny, out=outy2);

    //f
    And16(a=outx2, b=outy2, out=outand);
    Add16(a=outx2, b=outy2, out=outadd);
    Mux16(a=outand, b=outadd, sel=f, out=outf);

    //no
    Not16(in=outf, out=noutf);
    Mux16(a=outf, b=noutf, sel=no, out=out);

    //zr
    Mux16(a=outf, b=noutf, sel=no, out[0..7]=outv0, out[8..15]=outv1);
    Or8Way(in=outv0, out=zr0);
    Or8Way(in=outv1, out=zr1);
    Or(a=zr0, b=zr1, out=notzr);
    Not(in=notzr, out=zr);

    //ng
    Mux16(a=outf, b=noutf, sel=no, out[0..14]=devnull, out[15]=flagng);
    And(a=flagng, b=true, out=ng);
}
